load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("//python/private:util.bzl", "IS_BAZEL_7_OR_HIGHER")  # buildifier: disable=bzl-visibility
load("//sphinxdocs:sphinx.bzl", "sphinx_build_binary", "sphinx_docs")
load("//sphinxdocs:sphinx_stardoc.bzl", "sphinx_stardoc", "sphinx_stardocs")

sphinx_docs(
    name = "docs",
    srcs = glob(
        include = [
            "*.md",
        ],
    ),
    config = "conf.py",
    formats = [
        "html",
    ],
    renamed_srcs = {
        "//sphinxdocs/inventories:bazel_inventory": "bazel_inventory.inv",
    },
    sphinx = ":sphinx-build",
    strip_prefix = package_name() + "/",
    # We only develop the docs using Linux/Mac, and there are deps that
    # don't work for Windows, so just skip Windows.
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "@platforms//os:macos": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }) if IS_BAZEL_7_OR_HIGHER else ["@platforms//:incompatible"],
    deps = [
        ":bzl_function",
        ":bzl_providers",
        ":simple_bzl_docs",
    ],
)

sphinx_stardocs(
    name = "simple_bzl_docs",
    srcs = [":bzl_rule_bzl"],
    target_compatible_with = [] if IS_BAZEL_7_OR_HIGHER else ["@platforms//:incompatible"],
)

sphinx_stardoc(
    name = "bzl_function",
    src = ":bzl_function.bzl",
    deps = [":func_and_providers_bzl"],
)

sphinx_stardoc(
    name = "bzl_providers",
    src = ":bzl_providers.bzl",
    prefix = "addprefix_",
    deps = [":func_and_providers_bzl"],
)

# A bzl_library with multiple sources
bzl_library(
    name = "func_and_providers_bzl",
    srcs = [
        "bzl_function.bzl",
        "bzl_providers.bzl",
    ],
)

bzl_library(
    name = "bzl_rule_bzl",
    srcs = ["bzl_rule.bzl"],
    deps = [":func_and_providers_bzl"],
)

sphinx_build_binary(
    name = "sphinx-build",
    tags = ["manual"],  # Only needed as part of sphinx doc building
    deps = [
        "//sphinxdocs/src/sphinx_bzl",
        "@dev_pip//myst_parser",
        "@dev_pip//sphinx",
        "@dev_pip//typing_extensions",  # Needed by sphinx_stardoc
    ],
)
