# Copyright 2024 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("//tests/support:sh_py_run_test.bzl", "py_reconfig_test")

PYTHON_VERSIONS_TO_TEST = (
    "3.10",
    "3.11",
    "3.12",
)

[py_reconfig_test(
    name = "python_version_%s_test" % python_version,
    srcs = ["interpreter_test.py"],
    data = [
        "//python/bin:python",
    ],
    env = {
        # Both the interpreter and the test itself are expected to run under
        # the same version.
        "EXPECTED_INTERPRETER_VERSION": python_version,
        "EXPECTED_SELF_VERSION": python_version,
        "PYTHON_BIN": "$(rootpath //python/bin:python)",
    },
    main = "interpreter_test.py",
    python_version = python_version,
) for python_version in PYTHON_VERSIONS_TO_TEST]

[py_reconfig_test(
    name = "python_src_%s_test" % python_version,
    srcs = ["interpreter_test.py"],
    data = [
        "//python/bin:python",
    ],
    env = {
        # Since we're grabbing the interpreter from a binary with a fixed
        # version, we expect to always see that version. It doesn't matter what
        # Python version the test itself is running with.
        "EXPECTED_INTERPRETER_VERSION": "3.11",
        # The test itself is still running under different Python versions.
        "EXPECTED_SELF_VERSION": python_version,
        "PYTHON_BIN": "$(rootpath //python/bin:python)",
    },
    main = "interpreter_test.py",
    python_src = "//tools/publish:twine",
    python_version = python_version,
) for python_version in PYTHON_VERSIONS_TO_TEST]
