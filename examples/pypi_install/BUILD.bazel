load("@rules_python//python:defs.bzl", "py_binary", "py_test")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")

py_binary(
    name = "main",
    srcs = ["main.py"],
    deps = [
        "@pypi//requests",
        "@pypi//cognitojwt",
    ],
)

py_test(
    name = "test",
    srcs = ["test.py"],
    deps = [":main"],
)

# This rule adds a convenient way to update the requirements file.
compile_pip_requirements(
    name = "requirements",
    extra_args = ["--allow-unsafe"],
    requirements_in = "requirements.in",
    # Pass in a select-like value that lets the macro decide what to do with
    # the file. Having these values as dictionaries allows us to do some
    # validation before the underlying targets get instantiated.
    requirements_txt = {
        "@//:x86_64_linux": "requirements_lock_x86_64_linux.txt",
    },
    intermediate_file = {
        "@//:x86_64_linux": "intermediate_file_x86_64_linux.json",
    },
    # TODO(phil): Add an attribute that accepts a binary. That binary will let
    # users modify the intermediate file. Then they can:
    # - Inject dependencies.
    # - Remove dependencies.
    # - Drop whole packages (is there such a use case?).
    # - Add patches (support needed).
    # - Override/create aliases (support needed).
    intermediate_file_patcher = "//:intermediate_file_patcher",
)

py_binary(
    name = "intermediate_file_patcher",
    srcs = ["intermediate_file_patcher.py"],
)

config_setting(
    name = "x86_64_linux",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
    ],
)

genquery(
    name = "cognitojwt_deps",
    expression = """filter(@pypi//, kind(".* rule", deps(@pypi//cognitojwt)))""",
    scope = ["@pypi//cognitojwt"],
    opts = [
        "--notool_deps",
    ],
)

sh_test(
    name = "cognitojwt_deps_test",
    srcs = ["cognitojwt_deps_test.sh"],
    data = [
        ":cognitojwt_deps",
    ],
)
