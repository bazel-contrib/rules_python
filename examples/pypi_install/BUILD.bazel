load("@rules_python//python:defs.bzl", "py_binary", "py_test")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@bazel_skylib//lib:versions.bzl", "versions")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")

py_binary(
    name = "main",
    srcs = ["main.py"],
    deps = [
        "@pypi//pkg_a",
    ],
)

py_test(
    name = "test",
    srcs = ["test.py"],
    deps = [":main"],
)

# This rule adds a convenient way to update the requirements file.
compile_pip_requirements(
    name = "requirements",
    intermediate_file = {
        "@//:aarch64_linux": "intermediate_file_aarch64_linux.json",
        "@//:x86_64_linux": "intermediate_file_x86_64_linux.json",
    },
    # That binary will let users modify the intermediate file. Then they can:
    # - Inject dependencies.
    # - Remove dependencies.
    # - Drop whole packages (is there such a use case?).
    # - Add patches.
    # - Override/create aliases (support needed).
    intermediate_file_patcher = "//:intermediate_file_patcher",
    requirements_in = "requirements.in",
    # Pass in a select-like value that lets the macro decide what to do with
    # the file. Having these values as dictionaries allows us to do some
    # validation before the underlying targets get instantiated.
    requirements_txt = {
        "@//:aarch64_linux": "requirements_lock_aarch64_linux.txt",
        "@//:x86_64_linux": "requirements_lock_x86_64_linux.txt",
    },
    extra_args = [
        # Since we may frequently tweak the wheels for development, we can't
        # really rely on consistent hashes.
        "--no-reuse-hashes",
        # Point at the server run via:
        # bazel run //wheels:pypiserver_runner
        "--index-url",
        "http://localhost:8989/simple/",
    ],
)

py_binary(
    name = "intermediate_file_patcher",
    srcs = ["intermediate_file_patcher.py"],
)

config_setting(
    name = "aarch64_linux",
    constraint_values = [
        "@platforms//cpu:aarch64",
        "@platforms//os:linux",
    ],
)

config_setting(
    name = "x86_64_linux",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
    ],
)

genquery(
    name = "pkg_a_deps",
    expression = """filter(@pypi//, kind(".* rule", deps(@pypi//pkg_a)))""",
    opts = ["--notool_deps"],
    scope = ["@pypi//pkg_a"],
)

# TODO(phil): This doesn't work. Find a way to make this work.
#IS_BAZEL_6 = versions.parse(versions.get())[0] < 7

diff_test(
    name = "pkg_a_deps_test",
    file1 = ":pkg_a_deps",
    #file2 = "pkg_a_deps_bazel_6.txt" if IS_BAZEL_6 else "pkg_a_deps.txt",
    file2 = "pkg_a_deps.txt",
)

genquery(
    name = "pkg_d_deps",
    expression = """filter(@pypi//, kind(".* rule", deps(@pypi//pkg_d)))""",
    opts = ["--notool_deps"],
    scope = ["@pypi//pkg_d"],
)

diff_test(
    name = "pkg_d_deps_test",
    file1 = ":pkg_d_deps",
    #file2 = "pkg_d_deps_bazel_6.txt" if IS_BAZEL_6 else "pkg_d_deps.txt",
    file2 = "pkg_d_deps.txt",
)
