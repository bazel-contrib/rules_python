load("@bazel_skylib//rules:run_binary.bzl", "run_binary")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@python_versions//3.9:defs.bzl", py_console_script_binary_3_9 = "py_console_script_binary")
load("@rules_python//python:defs.bzl", "py_test")
load("@rules_python//python/entry_points:py_console_script_binary.bzl", "py_console_script_binary")

# This is how you can define a `pylint` entrypoint which uses the default python version.
py_console_script_binary(
    name = "pylint",
    pkg = "@pip//pylint",
)

# And a test that we can correctly run `pylint --version`
py_test(
    name = "pylint_test",
    srcs = ["pylint_test.py"],
    data = [":pylint"],
    env = {
        "ENTRY_POINT": "$(rlocationpath :pylint)",
    },
    deps = ["@rules_python//python/runfiles"],
)

# We can also specify extra dependencies for the binary, which is useful for
# tools like flake8, pylint, pytest, which have plugin discovery methods.
py_console_script_binary(
    name = "pylint_with_deps",
    pkg = "@pip//pylint",
    # Because `pylint` has multiple console_scripts available, we have to
    # specify which we want if the name of the target name 'pylint_with_deps'
    # cannot be used to guess the entry_point script.
    script = "pylint",
    deps = [
        # One can add extra dependencies to the entry point.
        "@pip//pylint_print",
    ],
)

# Here we create a file that has some pylint errors detected only with the
# pylint-print plugin.
write_file(
    name = "file_with_pylint_errors",
    out = "file_with_pylint_errors.py",
    content = [
        '"""',
        "a module to demonstrate the pylint-print checker",
        '"""',
        "",
        'if __name__ == "__main__":',
        '    print("Hello, World!")',
    ],
)

# Next run pylint on the file to generate a report.
run_binary(
    name = "pylint_report",
    srcs = [
        ":file_with_pylint_errors",
    ],
    outs = ["pylint_report.txt"],
    args = [
        "--output-format=text:$(location pylint_report.txt)",
        "--load-plugins=pylint_print",
        # The `exit-zero` ensures that `run_binary` is successful even though there are lint errors.
        # We check the generated report in the test below.
        "--exit-zero",
        "$(location :file_with_pylint_errors)",
    ],
    env = {
        # otherwise it may try to create ${HOME}/.cache/pylint
        "PYLINTHOME": "./.pylint_home",
    },
    tool = ":pylint_with_deps",
)

py_test(
    name = "pylint_deps_test",
    srcs = ["pylint_deps_test.py"],
    data = [
        ":pylint_report",
        ":pylint_with_deps",
    ],
    env = {
        "ENTRY_POINT": "$(rlocationpath :pylint_with_deps)",
        "PYLINT_REPORT": "$(rlocationpath :pylint_report)",
    },
    deps = ["@rules_python//python/runfiles"],
)

# A specific Python version can be forced by using the generated version-aware
# wrappers, e.g. to force Python 3.9:
py_console_script_binary_3_9(
    name = "yamllint",
    pkg = "@pip//yamllint:pkg",
)
