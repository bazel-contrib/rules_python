"""Repository rule to expose Python version from pyproject.toml."""

_TOML2JSON = Label("//tools/private/toml2json:toml2json.py")

def _parse_requires_python(requires_python):
    """Parse and validate the requires-python field."""
    if not requires_python.startswith("=="):
        fail("requires-python must use '==' for exact version, got: {}".format(requires_python))

    bare_version = requires_python[2:].strip()
    parts = bare_version.split(".")
    if len(parts) != 3:
        fail("requires-python must be in X.Y.Z format, got: {}".format(bare_version))
    for part in parts:
        if not part.isdigit():
            fail("requires-python must be in X.Y.Z format, got: {}".format(bare_version))

    return bare_version

def _pyproject_version_repo_impl(rctx):
    """Create a repository that exports PYTHON_VERSION from pyproject.toml."""
    pyproject_path = rctx.path(rctx.attr.pyproject_toml)
    rctx.read(pyproject_path, watch = "yes")

    toml2json = rctx.path(_TOML2JSON)
    result = rctx.execute([
        "python3",
        str(toml2json),
        str(pyproject_path),
    ])

    if result.return_code != 0:
        fail("Failed to parse pyproject.toml: " + result.stderr)

    data = json.decode(result.stdout)
    requires_python = data.get("project", {}).get("requires-python")
    if not requires_python:
        fail("pyproject.toml must contain [project] requires-python field")

    version = _parse_requires_python(requires_python)

    rctx.file("version.bzl", """\
\"\"\"Python version from pyproject.toml.

This file is automatically generated. Do not edit.
\"\"\"

PYTHON_VERSION = "{version}"
""".format(version = version))

    rctx.file("BUILD.bazel", """\
# Automatically generated from pyproject.toml
exports_files(["version.bzl"])
""")

pyproject_version_repo = repository_rule(
    implementation = _pyproject_version_repo_impl,
    attrs = {
        "pyproject_toml": attr.label(
            mandatory = True,
            doc = "Label pointing to pyproject.toml file.",
        ),
    },
    doc = """Repository rule that reads Python version from pyproject.toml.

This rule creates a repository with a `version.bzl` file that exports
`PYTHON_VERSION` constant.

Example:
```python
    load("@python_version_from_pyproject//:version.bzl", "PYTHON_VERSION")

    compile_pip_requirements(
        name = "requirements",
        python_version = PYTHON_VERSION,
        requirements_txt = "requirements.txt",
    )
```
""",
)
